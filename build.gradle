plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'maven-publish'
    id 'signing'
    id "pl.allegro.tech.build.axion-release" version "1.12.1"
    id "org.jetbrains.dokka" version "0.10.1"
}

repositories {
    jcenter()
}

tasks {
    dokka {
        outputFormat = "html"
        outputDirectory = "$buildDir/javadoc"
    }
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    implementation 'org.slf4j:slf4j-api:1.7.28'
    testImplementation 'org.slf4j:slf4j-simple:1.7.28'

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
}

task dokkaJar(type: Jar) {
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = "Assembles Kotlin docs with Dokka"
    archiveClassifier.set("javadoc")
    from(tasks.dokka)
    dependsOn(tasks.dokka)
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from(sourceSets.getByName("main").allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact dokkaJar

            pom {
                name = "ktee"
                description = "KTee is Tee for Kotlin code pipelines. If you love the unix command line tee, you know what we mean."
                url.set("https://github.com/medly/ktee")
                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id = "kdabir"
                        name = "Kunal Dabir"
                        email = "kd@medly.com"
                    }
                }
                scm {
                    connection = "scm:git:git@github.com:medly/ktee.git"
                    developerConnection = "scm:git:ssh://github.com/medly/ktee.git"
                    url = "https://github.com/medly/ktee"
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv("OSSRH_USERNAME")
                password System.getenv("OSSRH_PASSWORD")
            }

            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
        }
    }
}

scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
    }

    localOnly = true
}

group = "com.medly"
project.version = scmVersion.version

signing {
    def signingKey = System.getenv("OSSRH_SIGNING_SECRET_KEY")
    def signingPassword = System.getenv("OSSRH_SIGNING_PASSPHRASE")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
