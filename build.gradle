plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'maven-publish'
    id 'signing'
    id "pl.allegro.tech.build.axion-release" version "1.12.1"
    id "org.jetbrains.dokka" version "1.6.21"
}

repositories {
   mavenCentral()
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("$buildDir/javadoc"))
}

sourceSets {
    noop {
        java.srcDir 'src/noop/kotlin'
    }
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    implementation 'org.slf4j:slf4j-api:1.7.36'
    testImplementation 'org.slf4j:slf4j-simple:1.7.36'

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'

    noopImplementation 'org.slf4j:slf4j-nop:1.7.32'
    noopImplementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
}

task dokkaJar(type: Jar) {
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = "Assembles Kotlin docs with Dokka"
    archiveClassifier.set("javadoc")
    from(tasks.dokkaHtml)
    dependsOn(tasks.dokkaHtml)
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from(sourceSets.getByName("main").allSource)
}

task noopSources(type: Jar) {
    archiveClassifier.set("sources")
    from(sourceSets.noop.allSource)
}

task noopBinary(type: Jar) {
    archiveClassifier.set("")
    from(sourceSets.noop.output)
}

publishing {
    publications {
        noop(MavenPublication) {
            artifact noopSources
            artifact noopBinary

            pom {
                name = "ktee-noop"
                artifactId = "ktee-noop"
            }
        }

        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact dokkaJar

            pom {
                name = "ktee"
                description = "KTee is Tee for Kotlin code pipelines. " +
                        "If you love the unix command line tee, you know what we mean."
                url.set("https://github.com/medly/ktee")
                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id = "kdabir"
                        name = "Kunal Dabir"
                        email = "kd@medly.com"
                    }
                }
                scm {
                    connection = "scm:git:git@github.com:medly/ktee.git"
                    developerConnection = "scm:git:ssh://github.com/medly/ktee.git"
                    url = "https://github.com/medly/ktee"
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv("OSSRH_USERNAME")
                password System.getenv("OSSRH_PASSWORD")
            }

            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
        }
    }
}

scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
    }

    localOnly = true
}

group = "com.medly"
project.version = scmVersion.version

signing {
    def signingKey = System.getenv("OSSRH_SIGNING_SECRET_KEY")
    def signingPassword = System.getenv("OSSRH_SIGNING_PASSPHRASE")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
    sign publishing.publications.noop
}
